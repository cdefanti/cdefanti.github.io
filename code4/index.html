<script src=lib1.js></script>

<body bgcolor=black>
<center>
<td><canvas id='canvas1' width=400 height=400></canvas></td>
</center>
</body>

<script id='my_vertex_shader' type='x-shader/x-vertex'>
   attribute vec3 aPosition;
   varying   vec3 vPosition;
   void main() {
      gl_Position = vec4(aPosition, 1.0);
      vPosition = aPosition;
   }
</script>

<script id='my_fragment_shader' type='x-shader/x-fragment'>
   precision mediump float;
   uniform float uTime;
   uniform vec3  uCursor;
   varying vec3  vPosition;

   vec4 mSphere;
   vec4 wSphere;
   vec3 materials[2];

   vec3 Lrgb[1];
   vec3 Ldir[1];

   struct intersection {
     vec4 sphere1;
     vec4 sphere2;
   };
   intersection isec[5];

   bool inShadow(vec3 point);

   vec3 mod289(vec3 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
   vec4 mod289(vec4 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
   vec4 permute(vec4 x) { return mod289(((x*34.0)+1.0)*x); }
   vec4 taylorInvSqrt(vec4 r) { return 1.79284291400159 - 0.85373472095314 * r; }
   vec3 fade(vec3 t) { return t*t*t*(t*(t*6.0-15.0)+10.0); }

   float noise(vec3 P) {
     vec3 i0 = mod289(floor(P)), i1 = mod289(i0 + vec3(1.0));
     vec3 f0 = fract(P), f1 = f0 - vec3(1.0), f = fade(f0);
     vec4 ix = vec4(i0.x, i1.x, i0.x, i1.x), iy = vec4(i0.yy, i1.yy);
     vec4 iz0 = i0.zzzz, iz1 = i1.zzzz;
     vec4 ixy = permute(permute(ix) + iy), ixy0 = permute(ixy + iz0), ixy1 = permute(ixy + iz1);
     vec4 gx0 = ixy0 * (1.0 / 7.0), gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
     vec4 gx1 = ixy1 * (1.0 / 7.0), gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
     gx0 = fract(gx0); gx1 = fract(gx1);
     vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0), sz0 = step(gz0, vec4(0.0));
     vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1), sz1 = step(gz1, vec4(0.0));
     gx0 -= sz0 * (step(0.0, gx0) - 0.5); gy0 -= sz0 * (step(0.0, gy0) - 0.5);
     gx1 -= sz1 * (step(0.0, gx1) - 0.5); gy1 -= sz1 * (step(0.0, gy1) - 0.5);
     vec3 g0 = vec3(gx0.x,gy0.x,gz0.x), g1 = vec3(gx0.y,gy0.y,gz0.y),
          g2 = vec3(gx0.z,gy0.z,gz0.z), g3 = vec3(gx0.w,gy0.w,gz0.w),
          g4 = vec3(gx1.x,gy1.x,gz1.x), g5 = vec3(gx1.y,gy1.y,gz1.y),
          g6 = vec3(gx1.z,gy1.z,gz1.z), g7 = vec3(gx1.w,gy1.w,gz1.w);
     vec4 norm0 = taylorInvSqrt(vec4(dot(g0,g0), dot(g2,g2), dot(g1,g1), dot(g3,g3)));
     vec4 norm1 = taylorInvSqrt(vec4(dot(g4,g4), dot(g6,g6), dot(g5,g5), dot(g7,g7)));
     g0 *= norm0.x; g2 *= norm0.y; g1 *= norm0.z; g3 *= norm0.w;
     g4 *= norm1.x; g6 *= norm1.y; g5 *= norm1.z; g7 *= norm1.w;
     vec4 nz = mix(vec4(dot(g0, vec3(f0.x, f0.y, f0.z)), dot(g1, vec3(f1.x, f0.y, f0.z)),
           dot(g2, vec3(f0.x, f1.y, f0.z)), dot(g3, vec3(f1.x, f1.y, f0.z))),
         vec4(dot(g4, vec3(f0.x, f0.y, f1.z)), dot(g5, vec3(f1.x, f0.y, f1.z)),
           dot(g6, vec3(f0.x, f1.y, f1.z)), dot(g7, vec3(f1.x, f1.y, f1.z))), f.z);
     return 2.2 * mix(mix(nz.x,nz.z,f.y), mix(nz.y,nz.w,f.y), f.x);
   }

   // Compute intersection of a ray with a sphere, if any.  Return t.
   // If there is no intersection, return 10000.

   float raySphere(vec3 V, vec3 W, vec4 S) {
      
      // YOU NEED TO COMPUTE t, BY SETTING UP AND THEN SOLVING A QUADRATIC EQUATION.

      // a = 1.0, so we don't really need it..
      float b = 2.0 * (dot(W, V - vec3(S)));
      float c = (dot(V - vec3(S), V - vec3(S)) - S.w * S.w);
      if (b*b - 4.0 * c >= 0.0) {
        float val1 = (-b + sqrt(b * b - 4.0 * c)) / 2.0;
        float val2 = (-b - sqrt(b * b - 4.0 * c)) / 2.0;
        if (min(val1, val2) >= 0.0) {
          return min(val1, val2);
        } else {
          return max(val1, val2);
        }
      }

      return 10000.;
   }

   // Diffusely shade a sphere.
   //    point is the x,y,z position of the surface point.
   //    sphere is the x,y,z,r definition of the sphere.
   //    material is the r,g,b color of the sphere.

   vec3 shadeSphere(vec3 point, vec4 sphere, vec3 material) {
      vec3 normal = (point - sphere.xyz) / sphere.w;
      vec3 amb = material / 15.0;
      vec3 diff = vec3(0.0, 0.0, 0.0);
      vec3 spec = vec3(0.0, 0.0, 0.0);
      diff = diff + Lrgb[0] * material * max(0.0, dot(normal, Ldir[0]));
      vec3 eye = -normalize(point);
      vec3 r = 2.0 * dot(normal, Ldir[0]) * normal - Ldir[0];
      spec = spec + Lrgb[0] * material * pow(max(0.0, dot(eye, r)), 30.0);
      return amb + diff + spec;
   }

   vec3 shadeWaterSphere(vec3 point, vec3 dir, vec4 sphere, vec3 material) {
      vec3 dx = vec3(0.01, 0.0, 0.0);
      vec3 dy = vec3(0.0, 0.01, 0.0);
      vec3 base_point = point + vec3(0.0, 3.0 * uTime, 0.0);
      vec3 vnoise = 0.5 * (vec3(noise(base_point), noise(base_point + dx), noise(base_point + dy)) + 1.0);
      vec3 tangent = vec3(dx.x, 0.0, vnoise.y - vnoise.x);
      vec3 binormal = vec3(0.0, dy.y, vnoise.z - vnoise.x);
      vec3 normal = normalize(cross(tangent, binormal));
      vec3 amb = material / 15.0;
      //if (inShadow(point)) return amb;
      vec3 diff = vec3(0.0, 0.0, 0.0);
      vec3 spec = vec3(0.0, 0.0, 0.0);
      diff = diff + Lrgb[0] * material * max(0.0, dot(normal, Ldir[0]));
      vec3 eye = -normalize(point);
      vec3 r = 2.0 * dot(normal, Ldir[0]) * normal - Ldir[0];
      spec = spec + Lrgb[0] * material * pow(max(0.0, dot(eye, r)), 30.0);
      vec3 color = amb + diff + spec;

      // refract to hit other objects
      vec3 color2 = vec3(0.0, 0.0, 0.0);
      float t = 10000.0;
      vec3 V = point;
      float n = 1.33;
      vec3 W = n * cross(normal, cross(-normal, dir)) - normal * sqrt(1.0 - n * n * dot(cross(normal, dir), cross(normal, dir)));
      t = raySphere(V, W, mSphere);
      if (t < 10000.) {
        color2 = shadeSphere(V + W * t, mSphere, materials[1]);
      }


      return mix(color, color2, 0.8);
   }

   void main(void) {
      float M_PI = 3.1415926535897932384626433832795;
      vec2 c = uCursor.xy;

      Lrgb[0] = vec3(.4,.4,.4);
      Ldir[0] = normalize(vec3(1., 1., 1.));
      //Ldir[0] = normalize(vec3(c.x, c.y, 1. - 2. * dot(c, c)));

      // YOU NEED TO COMPUTE V AND W TO CREATE THE RAY FOR THIS PIXEL,
      // USING vPosition.x AND vPosition.y.

      vec3 V, W;
      V = vec3(0., 0., 0.);
      W = normalize(vec3(vPosition.x, vPosition.y, -1.0));

      wSphere = vec4(0.0, 0.0, -8.0, 4.0);
      mSphere = vec4(0.0, 0.0, -8.0, 3.0);

      // YOU NEED TO SET r,g,b FOR material.
      materials[0] = vec3(0.5, 0.5, 0.8);
      materials[1] = vec3(0.8, 0.1, 0.1);

      vec3 color = vec3(0.0, 0.0, 0.0);
      float t = 10000.0;
      t = raySphere(V, W, wSphere);
      if (t < 10000.) {
        color = shadeWaterSphere(V + W * t, W, wSphere, materials[0]);
      }
      gl_FragColor = vec4(sqrt(color), 1.); // Do Gamma correction.
   }
</script>

<script>
start_gl('canvas1', document.getElementById('my_vertex_shader'  ).innerHTML,
                    document.getElementById('my_fragment_shader').innerHTML);
</script>

